apply plugin: 'com.android.application'

android {

    rootProject.setDefaultConfig project

    defaultConfig {
        applicationId = "com.aide.financial"
        versionCode = 1
        versionName = "1.0.0"

        ndk {
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a'
            // 还可以添加 'x86', 'x86_64', 'mips', 'mips64'
        }

//        manifestPlaceholders = [
//                MTA_APPKEY : "A6R3WV3SI6CB",
//                MTA_CHANNEL : "debug_test",
//        ]
    }

    flavorDimensions ("channel")
    productFlavors {
        yingyongbao { dimension "channel" }
        huawei { dimension "channel" }
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [
//            applicationId : "com.aide.financial.${flavor.name}",
            CHANNEL : flavor.name,
            MTA_APPKEY : "A6R3WV3SI6CB",
            MTA_CHANNEL : flavor.name,
        ]
    }

    signingConfigs {
        release {
            keyAlias "key_bruce"
            keyPassword "bruce123"
            storeFile file("C:\\Users\\Bruce\\bruce.jks")
            storePassword "bruce123"
        }
    }

    buildTypes {
        release {
            minifyEnabled true // 打开混淆
            shrinkResources true // 资源压缩
            zipAlignEnabled true // 资源按照 4 字节对齐
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "LOG_DEBUG", "true"
            signingConfig signingConfigs.release //签名
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == 'release') {
            variant.outputs.all {
                variant.packageApplication.outputDirectory = new File(project.rootDir.absolutePath + File.separator + "outputs")
                outputFileName = "FNews_${variant.flavorName}_v${variant.versionName}_${time()}.apk"
            }
        }
    }
}

static def time() {
    return new Date().format('yyyyMMdd')
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation ('com.android.support.test:runner:1.0.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation ('com.android.support.test.espresso:espresso-core:3.0.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    // support
    implementation "com.android.support:support-v4:$rootProject.supportLibVersion"
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibVersion"
    implementation "com.android.support:support-annotations:$rootProject.supportLibVersion"
    //MTA
    implementation 'com.qq.mta:mta:3.4.2'
    implementation 'com.tencent.mid:mid:3.73-release'
}
